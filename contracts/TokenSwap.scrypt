import "util.scrypt";
import "rabin.scrypt";

contract TokenSwap {
  Ripemd160 tokenA_ScriptHash;
  Ripemd160 tokenB_ScriptHash;
  bytes contractIdA;
  bytes contractIdB;
  Ripemd160 issuerPKH;
  int issuer;

  function buildTokenOutput(bytes lockingScriptBody,
    int amount,
    int authCount,
    Ripemd160 holderPKH,
    int outValue
    ) : bytes {
    //OPRETUNR + '20' + amount(32bytes) + '01' + authCount(1byte) + '14' + holderPKH(20bytes) + '06' + brfc(6bytes) + '02' + stateLen(2bytes)

    // write Token UTXO locking script
    bytes scriptCode = lockingScriptBody + b'20' +  num2bin(amount, Util.TokenValueLen) + b'01' + num2bin(authCount, 1) + b'14' + holderPKH + Util.TOKEN_BRFC_ID + b'023f00';

    return Util.buildOutput(scriptCode, outValue);
  }

  public function swap(
    Ripemd160 buyerPKH,
    int tokenA_Amount,
    Ripemd160 sellerPKH,
    int tokenB_Amount,
    Ripemd160 changePKH,
    int changeTokenB_Amount,
    int notifySatoshi,
    int outValue,
    bytes prevOutput,
    bytes tokenA_CodeScript,
    bytes tokenB_CodeScript,
    SigHashPreimage txPreimage,
    int rabinSig, bytes rabinPadding) {

    require(tokenB_Amount > 0);
    require(tokenA_Amount > 0);

    SigHashType sigHashType = SigHash.ANYONECANPAY | SigHash.ALL | SigHash.FORKID;
    // this ensures the preimage is for the current tx
    require(Util.checkPreimageSigHashType(txPreimage, sigHashType));

    bytes scriptCode = Util.scriptCode(txPreimage);
    int scriptLen = len(scriptCode);
    int stateLenStart = scriptLen - 3; //'02' + stateLen(2bytes)
    int brfcStart = stateLenStart - 7; //'06' + brfc(6bytes)
    require(scriptCode[brfcStart : stateLenStart] == Util.SWAP_BRFC_ID); //b'06' + SWAP_BRFC_ID

    require(hash160(tokenA_CodeScript) == this.tokenA_ScriptHash);
    require(hash160(tokenB_CodeScript) == this.tokenB_ScriptHash);

    //outpoint (32-byte hash + 4-byte little endian)
    bytes prevOutpoint = Util.outpoint(txPreimage);

    //contractId + tokenAmount(4bytes)+satoshiAmount(4bytes)
    bytes msg = prevOutpoint + this.contractIdA + buyerPKH + num2bin(tokenA_Amount, Util.TokenValueLen) + this.contractIdB + sellerPKH + num2bin(tokenB_Amount, Util.TokenValueLen) + num2bin(changeTokenB_Amount, Util.TokenValueLen);
    // verify rabin signature
    require(Rabin.verifySig(msg, rabinSig, rabinPadding, this.issuer));

    //Swap Output
    bytes output0 = Util.buildOutput(Util.scriptCode(txPreimage), outValue);

    // Token A UTXO to Buyer
    bytes output1 = this.buildTokenOutput(tokenA_CodeScript, tokenA_Amount, 0, buyerPKH, outValue);

    // Token B UTXO to Seller
    bytes output2 = this.buildTokenOutput(tokenB_CodeScript, tokenB_Amount, 0, sellerPKH, outValue);

    // Token B change UTXO to changer
    bytes output3 = this.buildTokenOutput(tokenB_CodeScript, changeTokenB_Amount, 0, changePKH, outValue);

    // notify receiver output
    bytes output4 = Util.buildPublicKeyHashOutput(notifySatoshi, buyerPKH);

    Sha256 hashOutputs = hash256(prevOutput + output0 + output1 + output2 + output3 + output4);
    require(hashOutputs == Util.hashOutputs(txPreimage));
  }

  public function burn(Sig sig, PubKey pubKey) {
    require(hash160(pubKey) == this.issuerPKH);
    require(checkSig(sig, pubKey));
  }

}